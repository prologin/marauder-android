apply plugin: 'com.android.application'

File propFile = new File("$project.rootDir/local.properties")
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    props.each { prop ->
        project.ext.set(prop.key, prop.value)
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion project.ext.has("buildToolsVersion") ? project.ext["buildToolsVersion"] : "21.0.0"

    defaultConfig {
        applicationId "org.prologin.marauder"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 3
        versionName "1.2"

        resValue "string", "gcmAppId", "org.prologin.marauder"
    }
    buildTypes {
        debug {
            resValue "string", "buildName", "DEBUG"
            resValue "string", "baseApiUrl", project.ext.has("baseApiUrl") ? project.ext["baseApiUrl"] : "https://prologin.org"
            resValue "string", "gcmSenderId", project.ext.has("gcmSenderId") ? project.ext["gcmSenderId"] : "315661421577"
        }
        release {
            resValue "string", "buildName", "RELEASE"
            resValue "string", "baseApiUrl", "https://prologin.org"
            resValue "string", "gcmSenderId", "315661421577"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'org.jetbrains:annotations-java5:15.0'
}
